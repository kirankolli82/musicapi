buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		//classpath "nu.studer:gradle-jooq-plugin:8.3"
		//classpath "org.flywaydb:flyway-gradle-plugin:11.1.0"
		// Add PostgreSQL JDBC driver to the buildscript classpath so Flyway (which is
		// executed inside the Gradle JVM in the doFirst block) can load the
		// org.postgresql.Driver and recognise jdbc:postgresql URLs.
		classpath 'org.testcontainers:testcontainers:1.19.3'
		classpath 'org.testcontainers:postgresql:1.19.3'
		classpath 'org.flywaydb:flyway-core:10.4.1'
		classpath 'org.flywaydb:flyway-database-postgresql:10.4.1'
		classpath 'org.postgresql:postgresql:42.7.1'
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot'
	id 'io.spring.dependency-management'

	id 'nu.studer.jooq'
	id 'jvm-test-suite'
	id 'com.diffplug.spotless'
}

group = 'com.kiran'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

testing {
	suites {
		integrationTest(JvmTestSuite) {
			dependencies {
				implementation project()
				compileOnly 'org.projectlombok:lombok'
				annotationProcessor 'org.projectlombok:lombok'
			}

			targets {
				all {
					testTask.configure {
						useJUnitPlatform()
						shouldRunAfter(test)
					}
				}
			}
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
	systemProperty 'TESTCONTAINERS_RYUK_DISABLED', 'true'
}

spotless {
	java {
		target 'src/**/*.java'
		eclipse()
		removeUnusedImports()
		trimTrailingWhitespace()
		endWithNewline()
	}
}




dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	//implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.boot:spring-boot-starter-integration'
	implementation 'org.springframework.integration:spring-integration-core'
	compileOnly 'org.projectlombok:lombok'
	//runtimeOnly 'io.micrometer:micrometer-registry-otlp'
	// https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-starter-webmvc-ui
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
	// https://mvnrepository.com/artifact/org.jboss.resteasy/resteasy-client
	implementation 'org.jboss.resteasy:resteasy-client:6.2.14.Final'

	// https://mvnrepository.com/artifact/org.reflections/reflections
	implementation 'org.reflections:reflections:0.10.2'
	// Jackson JAX-RS provider so Resteasy client can use the Spring ObjectMapper to marshal/unmarshal JSON
	implementation 'org.jboss.resteasy:resteasy-jackson2-provider:7.0.0.Final'
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'org.flywaydb:flyway-core'
	runtimeOnly "org.postgresql:postgresql:42.7.1"

	implementation 'com.google.cloud:google-cloud-secretmanager:2.40.0'

	// Explicitly specify jOOQ generator dependencies with the same version
	jooqGenerator 'org.jooq:jooq-codegen:3.19.8'
	jooqGenerator 'org.jooq:jooq-meta:3.19.8'
	jooqGenerator 'org.postgresql:postgresql:42.7.1'
	jooqGenerator 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'

	annotationProcessor "org.projectlombok:lombok"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:kafka'
	testImplementation 'org.testcontainers:postgresql'
}


configurations.all {
	resolutionStrategy {
		force 'org.jooq:jooq:3.19.8'
		force 'org.jooq:jooq-codegen:3.19.8'
		force 'org.jooq:jooq-meta:3.19.8'
		force 'org.jooq:jooq-meta-extensions:3.19.8'
	}
}

configurations {
	integrationTestCompileOnly.extendsFrom compileOnly
	integrationTestImplementation.extendsFrom implementation
	integrationTestAnnotationProcessor.extendsFrom annotationProcessor
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

ext {
	postgresContainer = null
	containerJdbcUrl = null
	containerUsername = null
	containerPassword = null
}


//JOOQ


import org.flywaydb.core.Flyway
import org.testcontainers.containers.PostgreSQLContainer

task startPostgresContainer {
	doLast {
		println "Starting PostgreSQL Testcontainer..."

		postgresContainer = new PostgreSQLContainer('postgres:15-alpine')
		postgresContainer.start()

		containerJdbcUrl = postgresContainer.getJdbcUrl()
		containerUsername = postgresContainer.getUsername()
		containerPassword = postgresContainer.getPassword()

		println "PostgreSQL Testcontainer started:"
		println "  JDBC URL: ${containerJdbcUrl}"
		println "  Username: ${containerUsername}"
		println "  Password: ${containerPassword}"

		// Store values in project ext for other tasks
		project.ext.containerJdbcUrl = containerJdbcUrl
		project.ext.containerUsername = containerUsername
		project.ext.containerPassword = containerPassword
	}
}

task applyFlywayMigrations {
	dependsOn startPostgresContainer

	doLast {
		println "Applying Flyway migrations..."

		def flyway = Flyway.configure()
				.dataSource(
						project.ext.containerJdbcUrl,
						project.ext.containerUsername,
						project.ext.containerPassword
				)
				.locations("filesystem:${project.projectDir}/src/main/resources/db/migration")
				.load()

		def result = flyway.migrate()

		println "Flyway migrations applied successfully"
		println "  Migrations executed: ${result.migrationsExecuted}"

	}
}

jooq {
	configurations {
		main {
			generateSchemaSourceOnCompilation = false

			generationTool {
				jdbc {
					driver = 'org.postgresql.Driver'
					url = 'jdbc:postgresql://localhost:5432/placeholder'
					user = 'placeholder'
					password = 'placeholder'
				}
				generator {
					name = 'org.jooq.codegen.DefaultGenerator'
					database {
						name = 'org.jooq.meta.postgres.PostgresDatabase'
						inputSchema = 'refdata'
						excludes = 'flyway_schema_history'
					}
					generate {
						pojos = true
						fluentSetters = true
					}
					target {
						packageName = 'com.kiran.stockapi.jooq'
						directory = 'build/generated-src/jooq/main'
					}
				}
			}
		}
	}
}




// Dynamically configure jOOQ after container starts
tasks.named('generateJooq').configure {
	dependsOn applyFlywayMigrations

	finalizedBy 'stopPostgresContainer'

	doFirst {
		// Update JDBC configuration with actual container values
		jooq.configurations.main.jooqConfiguration.jdbc.url = project.ext.containerJdbcUrl
		jooq.configurations.main.jooqConfiguration.jdbc.user = project.ext.containerUsername
		jooq.configurations.main.jooqConfiguration.jdbc.password = project.ext.containerPassword

		println "jOOQ will connect to: ${project.ext.containerJdbcUrl}"
	}
}

task stopPostgresContainer {
	doLast {
		if (project.hasProperty('postgresContainer') && postgresContainer != null) {
			println "Stopping PostgreSQL Testcontainer..."
			postgresContainer.stop()
			println "PostgreSQL Testcontainer stopped"
		}
	}
}

ext.generateJooqBeforeCompile = project.hasProperty('generateJooq') ? project.property('generateJooq') : true

if (ext.generateJooqBeforeCompile) {
	tasks.named('compileJava') {
		dependsOn 'generateJooq'
	}
}

sourceSets {
	main {
		java {
			srcDir 'build/generated-src/jooq/main'
		}
	}
}

